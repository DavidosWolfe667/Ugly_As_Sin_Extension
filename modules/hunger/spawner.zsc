extend class UaS_Spawner {
	double spawnRate;
	vector3 spawnPos;

	//Set up spawns
	void HungerSpawns(WorldEvent e) {
		if(UaS_HungerTimeScale == 0 || UaS_HungerEnabled == false) { return; }
		if(level.maptime > 1) { return; }
		if(!e.Thing) { return; }
		if(e.Thing is "Inventory" && Inventory(e.Thing).Owner) { return; }

		spawnRate = UaS_HungerTimeScale * UaS_HungerRationRate;
		spawnRate = max(spawnRate, 0);
		spawnPos = e.Thing.pos;

		name toSpawnClass = 'UaS_FoodRation';
		if(UaS.RandomChance(0.5)) { toSpawnClass = 'UaS_WaterRation'; }

		//Spawn rations on backpacks, with a chance of an extra ration
		if(e.Thing.GetClassName() == "HDBackpack") {
			UaS.SpawnStuff('UaS_FoodRation', 1, spawnPos);
			UaS.SpawnStuff('UaS_WaterRation', 1, spawnPos);
		}

		//Randomly spawn on ammo boxes
		else if(e.Thing.GetClassName() == "HDAmBox" || e.Thing.GetClassName() == "HDAmBoxUnarmed") {
			MaybeSpawnRation(toSpawnClass, 0.005 * spawnRate);
		}

		//Randomly spawn on stimpacks
		else if(e.Thing.GetClassName() == "PortableStimpack") {
			MaybeSpawnRation(toSpawnClass, 0.0025 * spawnRate);
		}

		//Rarely spawn on medikits
		else if(e.Thing.GetClassName() == "PortableMedikit") {
			MaybeSpawnRation(toSpawnClass, 0.002 * spawnRate);
		}

		//Randomly spawn on marine corpses
		else if(e.Thing.GetClassName() == "DeadRifleman" || e.Thing.GetClassName() == "ReallyDeadRifleman") {
			MaybeSpawnRation(toSpawnClass, 0.002 * spawnRate);
		}
	}

	void MaybeSpawnRation(name spawnClass, double spawnChance) {
		if(UaS.RandomChance(clamp(spawnChance, 0, 1))) {
			UaS.SpawnStuff(spawnClass, 1, spawnPos);
			if(UaS_Debug & Hunger) { console.printf("Spawned Ration."); }
		}
	}
}
